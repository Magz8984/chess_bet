apply plugin: 'com.android.application'


android {
    compileSdkVersion 29
    defaultConfig {
        multiDexEnabled true
        minSdkVersion 16
        targetSdkVersion 29
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            ndkBuild {
                arguments '-j8'
            }
        }
    }
    packagingOptions {
        pickFirst 'META-INF/*'
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    flavorDimensions('app', 'env')
    productFlavors {
        dev {
            dimension 'env'
            versionCode 1
            versionName "1.0"
            applicationIdSuffix ".dev"
            buildConfigField("String", "CLOUD_FUNCTIONS_URL", cloudFunctionsTestURL)
            buildConfigField("String", "WEB_CLIENT_ID", webTestClientId)
            manifestPlaceholders = [env: "Test"]
        }

        prod {
            dimension 'env'
            buildConfigField("String", "CLOUD_FUNCTIONS_URL", cloudFunctionsURL)
            buildConfigField("String", "WEB_CLIENT_ID", webClientId)
            manifestPlaceholders = [env: ""]
        }

        chessbet {
            dimension 'app'
            applicationId 'com.chessbet.app'
            resValue "string", "app_name", "Chess Bet"
            versionCode 1
            versionName "1.0"
            manifestPlaceholders = [appNameValue: "Chess Bet"]
        }

        chessmvp { // Chess Bet Test
            dimension 'app'
            applicationId 'chessbet.app.com'
            resValue "string", "app_name", "Chess MVP"
            versionCode 16
            versionName "1.2.5"
            manifestPlaceholders = [appNameValue: "Chess MVP"]
        }
    }

    sourceSets {
        chessmvp {
            java.srcDirs('src/chessmvp/java', 'src/main/java')
        }
        chessbet {
            java.srcDirs('src/chessbet/java')
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        ndkBuild {
            path file('src/main/cpp/Android.mk')
        }
    }

    buildTypes.each {
        it.buildConfigField("String", "ADD_MOB_UNIT_ID", adUnitId)
        it.buildConfigField("String", "ADD_MOB_TEST_UNIT_ID", testAdUnitId)
        it.buildConfigField("String", "AD_MOB_APP_ID", adMobAppId)
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.core:core:1.2.0"

    implementation 'com.google.firebase:firebase-firestore:21.4.3'

    implementation 'com.google.android.material:material:1.2.0-alpha06'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.preference:preference:1.1.1"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation "androidx.annotation:annotation:1.1.0"
    api 'com.jakewharton:butterknife:10.2.1' // to prevent error: package butterknife does not exist
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
    implementation 'com.jakewharton:butterknife:10.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.google.firebase:firebase-auth:19.3.1'

    implementation 'com.google.firebase:firebase-database:19.3.0'
    implementation 'com.firebaseui:firebase-ui-firestore:6.1.0'
    implementation 'com.firebaseui:firebase-ui-database:6.1.0'

    implementation 'com.google.firebase:firebase-storage:19.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.12.6' // To support api levels less than 21
    implementation "com.squareup.okhttp3:logging-interceptor:3.12.6"

    implementation 'com.github.QuadFlask:colorpicker:0.0.13' // Color picker
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.github.iconic-king:chess-engine-dev:9eb56bdde9'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.erehmi:countdowntask:1.0.1' // Count Down Timer
    implementation 'com.github.jorgecastilloprz:fabprogresscircle:1.01@aar' // Fab Progress Bar
    implementation 'com.google.firebase:firebase-messaging:20.2.0'
    implementation 'com.michaelmuenzer.android:ScrollableNumberPicker:0.2.2' // Range Picker
    implementation "com.andkulikov:transitionseverywhere:1.8.1" // Transition everywhere
    implementation 'com.google.firebase:firebase-analytics:17.4.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.android.gms:play-services-ads:19.1.0'
    implementation 'com.google.android.gms:play-services-auth:18.0.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.github.GrenderG:Toasty:1.4.2'
}

task copyToAssets(type: Copy) {
    from('build/intermediates/ndkBuild/release/obj/local') {
        include '*/stockfish'
    }
    into 'src/main/assets'
}
tasks.withType(JavaCompile) {
    t -> t.dependsOn copyToAssets
}

apply plugin: 'com.google.gms.google-services'

apply plugin: 'com.google.firebase.crashlytics'
